# Description:
#   Tests for speak easy server

licenses(["notice"])  # Apache License 2.0

package(
    default_visibility = [
        "//services/speakeasy/javatests/androidx/test/services/speakeasy:__subpackages__",
    ],
)

SE_SERVER_TEST_SRCS = [
    "SpeakEasyTest.java",
]

SE_SERVER_DEPS = [
    "//runner/android_junit_runner",
    "//services/speakeasy/java/androidx/test/services/speakeasy:protocol",
    "//services/speakeasy/java/androidx/test/services/speakeasy/server:speak_easy_core",
]

EXTERNAL_DEPS = [
    "@maven//:com_google_code_findbugs_jsr305",
    "@maven//:junit_junit",
    "@maven//:com_google_truth_truth",
]

java_library(
    name = "test_deps",
    testonly = 1,
    visibility = ["//visibility:public"],
    exports = EXTERNAL_DEPS + SE_SERVER_DEPS,
)

android_library(
    name = "se_server_test_lib",
    testonly = 1,
    srcs = SE_SERVER_TEST_SRCS,
    manifest = "AndroidManifest_opensource.xml",
    deps = [":test_deps"],
)

android_binary(
    name = "se_server_binary",
    testonly = 1,
    manifest = "AndroidManifest_opensource_stub.xml",
)

android_binary(
    name = "se_server_test_binary",
    testonly = 1,
    instruments = ":se_server_binary",
    manifest = "AndroidManifest_opensource.xml",
    deps = [":se_server_test_lib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "server_tests_%s" % api_level,
    size = "large",
    args = [
        "--clear_package_data",
        "--install_test_services=True",
    ],
    target_device = "//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % (api_level),
    test_app = ":se_server_test_binary",
) for api_level in API_LEVELS]
